#
# Copyright (c) 2010, Ashok P. Nadkarni
# All rights reserved.
#
# See the file LICENSE for license

# This file contains tests for miscellaneous twapi commands

package require tcltest
eval tcltest::configure $argv

source [file join [file dirname [info script]] testutil.tcl]
load_twapi_package

#
# Set up system-specific constants that are used to match test results
namespace eval twapi::misc::test {
    namespace import ::tcltest::test
    ::tcltest::testConstraint win2k [twapi::min_os_version 5]

    ################################################################

    test twine-1.0 {
        Twine empty lists
    } -body {
        twapi::twine {} {}
    } -result {}

    test twine-1.1 {
        Twine one empty
    } -body {
        twapi::twine {a b} {}
    } -result {a {} b {}}

    test twine-1.2 {
        Twine equal lists
    } -body {
        twapi::twine {a b c} {1 2 3}
    } -result {a 1 b 2 c 3}

    test twine-1.3 {
        Twine unequal lists
    } -body {
        twapi::twine {a b} {1 2 3}
    } -result {a 1 b 2 {} 3}

    test twine-1.4 {
        Twine unequal lists
    } -body {
        twapi::twine {a b c} {1 2}
    } -result {a 1 b 2 c {}}

    ################################################################

    test kl_create-1.0 {
        Create a keyed list
    } -body {
        twapi::kl_create a 1 b 2 c 3
    } -result {a 1 b 2 c 3}

    test kl_create-1.1 {
        Create a badly formed keyed list
    } -body {
        twapi::kl_create a 1 b 2 c
    } -result * -match glob -returnCodes error

    test kl_create2-1.0 {
        Create a keyed list
    } -body {
        twapi::kl_create2 {a b c} {1 2 3}
    } -result {a 1 b 2 c 3}

    test kl_get-1.0 {
        Get a value from a keyed list (first)
    } -body {
        twapi::kl_get [twapi::kl_create a 1 b 2 c 3] a
    } -result 1

    test kl_get-1.1 {
        Get a value from a keyed list (last)
    } -body {
        twapi::kl_get [twapi::kl_create a 1 b 2 c 3] c
    } -result 3

    test kl_get-1.2 {
        Get a value from a keyed list (middle)
    } -body {
        twapi::kl_get [twapi::kl_create a 1 b 2 c 3] b
    } -result 2

    test kl_get-2.0 {
        Get a non-existent value from a keyed list with default
    } -body {
        twapi::kl_get [twapi::kl_create a 1 b 2 c 3] d 42
    } -result 42

    test kl_get-2.1 {
        Get a non-existent value from a keyed list
    } -body {
        twapi::kl_get [twapi::kl_create a 1 b 2 c 3] d
    } -result * -match glob -returnCodes error
    
    test kl_vget-1.0 {
        Store a value from a keyed list into a variable
    } -body {
        set status [twapi::kl_vget [twapi::kl_create a 1 b 2 c 3] c var]
        list $status $var
    } -result {1 3}

    test kl_vget-1.1 {
        Try to get a non-existing value from a keyed list 
    } -body {
        set var unchanged
        set status [twapi::kl_vget [twapi::kl_create a 1 b 2 c 3] d var]
        list $status $var
    } -result {0 unchanged}

    test kl_vget-1.2 {
        Try to get a non-existing value from a keyed list 
    } -body {
        catch {unset var}
        set status [twapi::kl_vget [twapi::kl_create a 1 b 2 c 3] d var]
        list $status [info exists var]
    } -result {0 0}

    test kl_equal-1.0 {
        Compare empty keyed lists
    } -body {
        twapi::kl_equal [twapi::kl_create] [twapi::kl_create2 {} {}]
    } -result 1

    test kl_equal-1.1 {
        Compare empty keyed list with non-empty
    } -body {
        twapi::kl_equal [twapi::kl_create a 1 b 2] [twapi::kl_create2 {} {}]
    } -result 0

    test kl_equal-1.2 {
        Compare keyed list (identical)
    } -body {
        twapi::kl_equal [twapi::kl_create a 1 b 2 c 3] [twapi::kl_create2 {a b c} {1 2 3}]
    } -result 1

    test kl_equal-1.3 {
        Compare keyed list (out of order)
    } -body {
        twapi::kl_equal [twapi::kl_create a 1 c 3 b 2] [twapi::kl_create2 {a b c} {1 2 3}]
    } -result 1

    test kl_equal-1.4 {
        Compare keyed list (keys in order, different values)
    } -body {
        twapi::kl_equal [twapi::kl_create a 1 c 3 b 2] [twapi::kl_create a 1 b 2 c 4]
    } -result 0

    test kl_equal-1.5 {
        Compare keyed list (different lengths)
    } -body {
        twapi::kl_equal [twapi::kl_create a 1 b 2 c 3] [twapi::kl_create a 1 b 2]
    } -result 0

    test kl_fields-1.0 {
        Get fields in a keyed list
    } -body {
        twapi::kl_fields [twapi::kl_create2 {a b c} {1 2 3}]
    } -result {a b c}

    test kl_flatten-1.0 {
        Flatten values in a list of keyed lists
    } -body {
        twapi::kl_flatten [list [twapi::kl_create a 1 b 2] [twapi::kl_create a 3 b 4]] a
    } -result {1 3}

    test kl_flatten-1.1 {
        Flatten values in a list of keyed lists (multiple fields, multiple lists)
    } -body {
        twapi::kl_flatten [list [twapi::kl_create a 1 b 2 c 3] [twapi::kl_create a 3 b 4 c 5] [twapi::kl_create a 6 b 7 c 8]] a c
    } -result {1 3 3 5 6 8}

    test kl_set-1.0 {
        Set a non-existing value in a keyed list
    } -body {
        twapi::kl_equal [twapi::kl_set [twapi::kl_create a 1 b 2] c 3] \
            [twapi::kl_create a 1 b 2 c 3]
    } -result 1

    test kl_set-1.1 {
        Set an existing value in a keyed list
    } -body {
        twapi::kl_equal [twapi::kl_set [twapi::kl_create a 1 b 2 c 4] c 3] \
            [twapi::kl_create a 1 b 2 c 3]
    } -result 1

    test kl_unset-1.0 {
        Unset an existing value in a keyed list
    } -body {
        twapi::kl_equal [twapi::kl_unset [twapi::kl_create a 1 b 2 c 3] b] \
            [twapi::kl_create a 1 c 3]
    } -result 1

    test kl_unset-1.1 {
        Unset a non-existing value in a keyed list
    } -body {
        twapi::kl_equal [twapi::kl_unset [twapi::kl_create a 1 b 2 c 3] d] \
            [twapi::kl_create a 1 c 3 b 2]
    } -result 1

    ################################################################

    test list_raw_api-1.0 {
        List the raw API
    } -body {
        expr {[llength [twapi::list_raw_api]] >= 500}
    } -result 1

    ################################################################

    test add_defines-1.0 {
        Verify add_defines adds a define to windefs
    } -body {
        twapi::add_defines {TWAPI_TEST_DEFINE 1234}
        set ::twapi::windefs(TWAPI_TEST_DEFINE)
    } -result 1234


    ################################################################

    test debuglog-1.0 {
        Verify debug log messages
    } -body {
        twapi::debuglog "Test message 1"
        twapi::debuglog "Test message 2"
        lrange [twapi::debuglog] end-1 end
    } -result {{Test message 1} {Test message 2}}

    ################################################################

    test debuglog_clear-1.0 {
        Clear debug log messages
    } -body {
        twapi::debuglog "Test message 1"
        twapi::debuglog "Test message 2"
        twapi::debuglog_clear
        llength [twapi::debuglog]
    } -result 0

    ################################################################

    test tcltype-1.0 {
        Test for pure string type
    } -body {
        twapi::tcltype "abc"
    } -result ""

    test tcltype-1.1 {
        Test for pure string type (numeric)
    } -body {
        twapi::tcltype 100
    } -result ""

    test tcltype-2.0 {
        Test for  string type
    } -body {
        set temp "abc"
        append temp d;          # Force to explicit string
        twapi::tcltype $temp
    } -result string

    test tcltype-3.0 {
        Test for  list type
    } -body {
        twapi::tcltype [list a b]
    } -result list

    test tcltype-4.0 {
        Test for  dict type
    } -body {
        twapi::tcltype [dict create a b c d]
    } -result dict

    test tcltype-5.0 {
        Test for integer type
    } -body {
        twapi::tcltype [expr {0+0}]
    } -result int

    test tcltype-6.0 {
        Test for wide integer type
    } -body {
        twapi::tcltype [expr {0xffffffff+1}]
    } -result wideInt

    test tcltype-7.0 {
        Test for double type
    } -body {
        twapi::tcltype [expr {1.0+0}]
    } -result double

    test tcltype-8.0 {
        Test for boolean type
    } -body {
        twapi::tcltype [expr {false}]
    } -result booleanString

    test tcltype-8.1 {
        Test for boolean type
    } -body {
        twapi::tcltype [expr {true}]
    } -result booleanString


    ################################################################

    test tclcast-1.0 {
        Test for pure string type
    } -body {
        twapi::tcltype [twapi::tclcast "" abc]
    } -result ""

    test tclcast-2.0 {
        Test for  string type
    } -body {
        twapi::tcltype [twapi::tclcast string [expr {1+0}]]
    } -result string

    test tclcast-3.0 {
        Test for  list type
    } -body {
        twapi::tcltype [twapi::tclcast list "x y z"]
    } -result list

    test tclcast-4.0 {
        Test for  dict type
    } -body {
        twapi::tcltype [twapi::tclcast dict "m n o p"]
    } -result dict

    test tclcast-5.0 {
        Test for integer type
    } -body {
        twapi::tcltype [twapi::tclcast int "123"]
    } -result int

    test tclcast-6.0 {
        Test for wide integer type
    } -body {
        # Note casting a 32 bit int to a wide int will not result
        # in a wide int - Tcl optimizes it back to an int
        twapi::tcltype [twapi::tclcast wideInt "0x123456789abcd"]
    } -result wideInt

    test tclcast-7.0 {
        Test for double type
    } -body {
        twapi::tcltype [twapi::tclcast double "2.5"]
    } -result double

    test tclcast-8.0 {
        Test for boolean type
    } -body {
        twapi::tcltype [twapi::tclcast boolean "true"]
    } -result booleanString

    test tclcast-8.1 {
        Test for boolean type
    } -body {
        twapi::tcltype [twapi::tclcast boolean "false"]
    } -result booleanString

    test tclcast-8.2 {
        Test for boolean type
    } -body {
        twapi::tcltype [twapi::tclcast boolean [expr {0+1}]]
    } -result booleanString

    test tclcast-8.3 {
        Test for boolean type
    } -body {
        twapi::tcltype [twapi::tclcast boolean [expr {0+0}]]
    } -result booleanString


    ################################################################

    #
    # Clean up
    ::tcltest::cleanupTests
}

namespace delete ::twapi::misc::test
