#
# Copyright (c) 2004, Ashok P. Nadkarni
# All rights reserved.
#
# See the file LICENSE for license

# This file contains tests for the parseargs command

package require tcltest
eval tcltest::configure $argv

source [file join [file dirname [info script]] testutil.tcl]
load_twapi_package twapi_base


namespace eval twapi::parseargs::test {
    namespace import ::tcltest::test

    ################################################################

    test parseargs-1.0 {
        Verify parsing of empty argument list
    } -body {
        set vargs {}
        twapi::parseargs vargs {x.arg y}
    } -result "y 0"

    test parseargs-1.1 {
        Verify parsing of arguments with empty option list
    } -body {
        unset -nocomplain opts
        set vargs {arg1 arg2}
        set opts [twapi::parseargs vargs {}]
        expr {[string length $opts] == 0 && [string equal $vargs "arg1 arg2"]}
    } -result 1

    test parseargs-1.2 {
        Verify parsing of arguments with multiple character option names
    } -body {
        unset -nocomplain opts
        set vargs {-option 1 extraarg}
        array set opts [twapi::parseargs vargs {{option.arg} {b.arg}}]
        expr {
              [llength $vargs] == 1 &&
              [string equal [lindex $vargs 0] "extraarg"] &&
              $opts(option) == 1 &&
              ![info exists opts(b)]}
    } -result 1

    test parseargs-1.3 {
        Verify case sensitivity of option names
    } -body {
        unset -nocomplain opts
        set vargs {-option optvalue -Option OPTVALUE extraarg}
        array set opts [twapi::parseargs vargs {
            {option.arg} {Option.arg OPTIONB}
        }]
        expr {
              [llength $vargs] == 1 &&
              [string equal [lindex $vargs 0] "extraarg"] &&
              $opts(option) eq "optvalue" &&
              $opts(Option) eq "OPTVALUE"
          }
    } -result 1

    test parseargs-1.4 {
        Verify parsing of arguments with missing argument values
    } -body {
        unset -nocomplain opts
        set vargs {-a}
        set opts [twapi::parseargs vargs {a.arg}]
    } -returnCodes {error} -result "No value supplied for option '-a'"

    test parseargs-1.5 {
        Verify parsing of arguments of invalid types
    } -body {
        unset -nocomplain opts
        set vargs {-a}
        set opts [twapi::parseargs vargs {a.foo}]
    } -returnCodes {error} -result "Badly formed option descriptor: 'a.foo'"

    test parseargs-2.0 {
        Verify parsing of arguments of type any
    } -body {
        unset -nocomplain opts
        set vargs {-a 1 extraarg}
        array set opts [twapi::parseargs vargs {{a.arg} {b.arg}}]
        expr {
              [llength $vargs] == 1 &&
              [string equal [lindex $vargs 0] "extraarg"] &&
              $opts(a) == 1 &&
              ![info exists opts(b)]}
    } -result 1

    test parseargs-2.1 {
        Verify parsing of arguments of type any with default arguments
    } -body {
        unset -nocomplain opts
        set vargs {-a avalue extraarg}
        array set opts [twapi::parseargs vargs {{a.arg adefault} {b.arg bdefault}}]
        expr {
              [llength $vargs] == 1 &&
              [string equal [lindex $vargs 0] "extraarg"] &&
              $opts(a) eq "avalue" &&
              $opts(b) eq "bdefault"}
    } -result 1

    test parseargs-2.2 {
        Verify parsing of arguments of type any matching enumerated values
    } -body {
        unset -nocomplain opts
        set vargs {-a avalue -b bvalue -c cvalue extraarg}
        # Note match for a occurs in first element, for b in second element
        # for c in last element
        array set opts [twapi::parseargs vargs {
            {a.arg adefault {avalue xvalue yvalue }}
            {b.arg bdefault {xvalue bvalue zvalue}}
            {c.arg cdefault {xvalue bvalue cvalue}}
        }]
        expr {
              [llength $vargs] == 1 &&
              [string equal [lindex $vargs 0] "extraarg"] &&
              $opts(a) eq "avalue" &&
              $opts(c) eq "cvalue" &&
              $opts(b) eq "bvalue"}
    } -result 1

    test parseargs-2.3 {
        Verify parsing of arguments of type any not matching enumerated values
    } -body {
        unset -nocomplain opts
        set vargs {-a avalue -b bvalue -c cvalue extraarg}
        # Note match for a occurs in first element, for b in second element
        # for c in last element
        array set opts [twapi::parseargs vargs {
            {a.arg adefault {zvalue xvalue yvalue}}
            {b.arg bdefault {xvalue bvalue zvalue}}
            {c.arg cdefault {xvalue bvalue cvalue}}
        }]
    } -returnCodes {error} -result "Invalid value 'avalue' specified for option '-a'. Must be one of zvalue, xvalue, yvalue"

    test parseargs-2.4 {
        Verify parsing of missing options of type any with -nulldefault
    } -body {
        unset -nocomplain opts
        set vargs {-a avalue extraarg}
        array set opts [twapi::parseargs vargs {
            {a.arg adefault {avalue xvalue yvalue}}
            b.arg
        } -nulldefault]
        set opts(b)
    } -result ""

    test parseargs-3.0 {
        Verify parsing of arguments of type integer
    } -body {
        unset -nocomplain opts
        set vargs {-a 1 extraarg}
        array set opts [twapi::parseargs vargs {{a.int} {b.int}}]
        expr {
              [llength $vargs] == 1 &&
              [string equal [lindex $vargs 0] "extraarg"] &&
              $opts(a) == 1 &&
              ![info exists opts(b)]}
    } -result 1

    test parseargs-3.1 {
        Verify parsing of arguments of type int with default arguments
    } -body {
        unset -nocomplain opts
        set vargs {-a 1 extraarg}
        array set opts [twapi::parseargs vargs {{a.int 2} {b.int 100}}]
        expr {
              [llength $vargs] == 1 &&
              [string equal [lindex $vargs 0] "extraarg"] &&
              $opts(a) == 1 &&
              $opts(b) == 100}
    } -result 1

    test parseargs-3.2 {
        Verify parsing of arguments of type int that are negative
    } -body {
        unset -nocomplain opts
        set vargs {-a -100 extraarg}
        array set opts [twapi::parseargs vargs {{a.int 2} {b.int 100}}]
        expr {
              [llength $vargs] == 1 &&
              [string equal [lindex $vargs 0] "extraarg"] &&
              $opts(a) == -100 &&
              $opts(b) == 100}
    } -result 1

    test parseargs-3.3 {
        Verify parsing of arguments of type int that are hex
    } -body {
        unset -nocomplain opts
        set vargs {-a 0x100 extraarg}
        array set opts [twapi::parseargs vargs {{a.int 2} {b.int 0xa0}}]
        expr {
              [llength $vargs] == 1 &&
              [string equal [lindex $vargs 0] "extraarg"] &&
              $opts(a) == 256 &&
              $opts(b) == 160}
    } -result 1

    test parseargs-3.4 {
        Verify parsing of arguments of type int that are octal
    } -body {
        unset -nocomplain opts
        set vargs {-a 0123 extraarg}
        array set opts [twapi::parseargs vargs {{a.int 2} {b.int 010}}]
        expr {
              [llength $vargs] == 1 &&
              [string equal [lindex $vargs 0] "extraarg"] &&
              $opts(a) == 83 &&
              $opts(b) == 8}
    } -result 1

    test parseargs-3.5 {
        Verify parsing of invalid arguments of type int
    } -body {
        unset -nocomplain opts
        set vargs {-a not_an_int extraarg}
        array set opts [twapi::parseargs vargs {{a.int 2} {b.int 100}}]
    } -returnCodes {error} -result "Non-integer value 'not_an_int' specified for option '-a'"

    test parseargs-3.6 {
        Verify parsing of arguments of type int matching enumerated values
    } -body {
        unset -nocomplain opts
        set vargs {-a 1 -b 2 -c 3 extraarg}
        # Note match for a occurs in first element, for b in second element
        # for c in last element
        array set opts [twapi::parseargs vargs {
            {a.int 0 {1 2 3}}
            {b.int 0 {1 2 3}}
            {c.int 0 {1 2 3}}
        }]
        expr {
              [llength $vargs] == 1 &&
              [string equal [lindex $vargs 0] "extraarg"] &&
              $opts(a) == 1 &&
              $opts(b) == 2 &&
              $opts(c) == 3
          }
    } -result 1

    test parseargs-3.7 {
        Verify parsing of arguments of type int not matching enumerated values
    } -body {
        unset -nocomplain opts
        set vargs {-a 0 -b 2 -c 3 extraarg}
        # Note match for a occurs in first element, for b in second element
        # for c in last element
        array set opts [twapi::parseargs vargs {
            {a.arg 0 {1 2 3}}
            {b.arg 0 {1 2 3}}
            {c.arg 0 {1 2 3}}
        }]
    } -returnCodes {error} -result "Invalid value '0' specified for option '-a'. Must be one of 1, 2, 3"

    test parseargs-3.8 {
        Verify parsing of missing options of type int with -nulldefault
    } -body {
        unset -nocomplain opts
        set vargs {-a avalue extraarg}
        array set opts [twapi::parseargs vargs {
            {a.arg adefault {avalue xvalue yvalue}}
            b.int
        } -nulldefault]
        set opts(b)
    } -result 0

    test parseargs-4.0 {
        Verify parsing of arguments of type boolean (integer true)
    } -body {
        unset -nocomplain opts
        set vargs {-a 100 extraarg}
        array set opts [twapi::parseargs vargs {{a.bool} {b.int}}]
        expr {
              [llength $vargs] == 1 &&
              [string equal [lindex $vargs 0] "extraarg"] &&
              $opts(a) == 1 &&
              ![info exists opts(b)]}
    } -result 1

    test parseargs-4.1 {
        Verify parsing of arguments of type boolean (integer false)
    } -body {
        set vargs {-a 0 extraarg}
        array set opts [twapi::parseargs vargs {{a.bool} {b.int}}]
        expr {
              [llength $vargs] == 1 &&
              [string equal [lindex $vargs 0] "extraarg"] &&
              $opts(a) == 0 &&
              ![info exists opts(b)]}
    } -result 1

    test parseargs-4.2 {
        Verify parsing of arguments of type boolean (string true)
    } -body {
        unset -nocomplain opts
        set vargs {-a true extraarg}
        array set opts [twapi::parseargs vargs {{a.bool} {b.int}}]
        expr {
              [llength $vargs] == 1 &&
              [string equal [lindex $vargs 0] "extraarg"] &&
              $opts(a) == 1 &&
              ![info exists opts(b)]}
    } -result 1

    test parseargs-4.3 {
        Verify parsing of arguments of type boolean (string false)
    } -body {
        unset -nocomplain opts
        set vargs {-a false extraarg}
        array set opts [twapi::parseargs vargs {{a.bool} {b.int}}]
        expr {
              [llength $vargs] == 1 &&
              [string equal [lindex $vargs 0] "extraarg"] &&
              $opts(a) == 0 &&
              ![info exists opts(b)]}
    } -result 1

    test parseargs-4.4 {
        Verify parsing of arguments of type boolean (string on)
    } -body {
        unset -nocomplain opts
        set vargs {-a on extraarg}
        array set opts [twapi::parseargs vargs {{a.bool} {b.int}}]
        expr {
              [llength $vargs] == 1 &&
              [string equal [lindex $vargs 0] "extraarg"] &&
              $opts(a) == 1 &&
              ![info exists opts(b)]}
    } -result 1

    test parseargs-4.5 {
        Verify parsing of arguments of type boolean (string off)
    } -body {
        unset -nocomplain opts
        set vargs {-a off extraarg}
        array set opts [twapi::parseargs vargs {{a.bool} {b.int}}]
        expr {
              [llength $vargs] == 1 &&
              [string equal [lindex $vargs 0] "extraarg"] &&
              $opts(a) == 0 &&
              ![info exists opts(b)]}
    } -result 1

    test parseargs-4.6 {
        Verify parsing of default arguments of type boolean
    } -body {
        unset -nocomplain opts
        set vargs {extraarg anotherarg}
        array set opts [twapi::parseargs vargs {{a.bool true} {b.bool 0}}]
        expr {
              [llength $vargs] == 2 &&
              [string equal [lindex $vargs 0] "extraarg"] &&
              [string equal [lindex $vargs 1] "anotherarg"] &&
              $opts(a) &&
              ! $opts(b)}
    } -result 1

    test parseargs-4.7 {
        Verify parsing of invalid arguments of type bool
    } -body {
        unset -nocomplain opts
        set vargs {-a not_a_bool extraarg}
        array set opts [twapi::parseargs vargs {{a.bool 1} {b.int 100}}]
    } -returnCodes {error} -result "Non-boolean value 'not_a_bool' specified for option '-a'"

    test parseargs-4.8 {
        Verify parsing of arguments of type bool return mapped true value
    } -body {
        unset -nocomplain opts
        set vargs {-a true extraarg}
        array set opts [twapi::parseargs vargs {
            {a.bool 0 mappedvalue}
        }]
        list [llength $vargs] $opts(a)
    } -result {1 mappedvalue}

    test parseargs-4.9 {
        Verify parsing of missing options of type bool with -nulldefault
    } -body {
        unset -nocomplain opts
        set vargs {-a avalue extraarg}
        array set opts [twapi::parseargs vargs {
            {a.arg adefault {avalue xvalue yvalue}}
            b.bool
        } -nulldefault]
        set opts(b)
    } -result 0

    test parseargs-5.0 {
        Verify parsing of arguments of type switch
    } -body {
        unset -nocomplain opts
        set vargs {-a extraarg}
        array set opts [twapi::parseargs vargs {{b.switch} {a.switch}}]
        expr {
              [llength $vargs] == 1 &&
              [string equal [lindex $vargs 0] "extraarg"] &&
              $opts(a) &&
              !$opts(b)}
    } -result 1

    test parseargs-5.1 {
        Verify parsing of arguments of type switch ignore defaults
    } -body {
        unset -nocomplain opts
        set vargs {extraarg}
        array set opts [twapi::parseargs vargs {{b.switch} {a.switch 1}}]
        expr {
              [llength $vargs] == 1 &&
              [string equal [lindex $vargs 0] "extraarg"] &&
              !$opts(a) &&
              !$opts(b)
          }
    } -result 1

    test parseargs-5.2 {
        Verify options with no specifiers are treated as type switch
    } -body {
        unset -nocomplain opts
        set vargs {-a arg extraarg}
        array set opts [twapi::parseargs vargs {a b}]
        expr {
              [llength $vargs] == 2 &&
              [string equal [lindex $vargs 0] "arg"] &&
              [string equal [lindex $vargs 1] "extraarg"] &&
              $opts(a) &&
              !$opts(b)
          }
    } -result 1

    test parseargs-5.3 {
        Verify parsing of arguments of type switch return mapped values
    } -body {
        unset -nocomplain opts
        set vargs {-a arg extraarg}
        array set opts [twapi::parseargs vargs {
            {a 0 mappedvalue}
        }]

        list [llength $vargs] {*}$vargs $opts(a)
    } -result {2 arg extraarg mappedvalue}

    test parseargs-6.0 {
        Verify parsing of invalid options
    } -body {
        unset -nocomplain opts
        set vargs {-a 1 -b 2 extraarg}
        array set opts [twapi::parseargs vargs {b.arg c.int}]
    } -returnCodes {error} -result "Invalid option '-a'. Must be one of -b, -c"

    test parseargs-6.1 {
        Verify invalid options without arguments are ignored with -ignoreunknown
    } -body {
        unset -nocomplain opts
        set vargs {-a -b 2 extraarg}
        array set opts [twapi::parseargs vargs {{b.arg}} -ignoreunknown]
        expr {
              [llength $vargs] == 2 &&
              [string equal [lindex $vargs 0] "-a"] &&
              [string equal [lindex $vargs 1] "extraarg"] &&
              $opts(b)}
    } -result 1

    test parseargs-6.2 {
        Verify invalid options with arguments are ignored with -ignoreunknown
    } -body {
        unset -nocomplain opts
        set vargs {-a 1 -b 2 extraarg}
        array set opts [twapi::parseargs vargs {{b.arg}} -ignoreunknown]
        expr {
              [llength $vargs] == 3 &&
              [string equal [lindex $vargs 0] "-a"] &&
              [string equal [lindex $vargs 1] "1"] &&
              [string equal [lindex $vargs 2] "extraarg"] &&
              $opts(b)}
    } -result 1

    test parseargs-7.0 {
        Verify parsing of leftover arguments (none allowed)
    } -setup {
        unset -nocomplain opts
        set vargs {-b 2 leftoverA}
    } -body {
        array set opts [twapi::parseargs vargs {{b.arg}} -maxleftover 0]
    } -returnCodes {error} -result "Command has extra arguments specified: leftoverA"

    test parseargs-7.1 {
        Verify parsing of leftover arguments (more than allowed)
    } -setup {
        unset -nocomplain opts
        set vargs {-b 2 leftoverA leftoverB}
    } -body {
        array set opts [twapi::parseargs vargs {{b.arg}} -maxleftover 1]
    } -returnCodes {error} -result "Command has extra arguments specified: leftoverA leftoverB"

    test parseargs-7.2 {
        Verify parsing of leftover arguments (exactly what's than allowed)
    } -setup {
        unset -nocomplain opts
        set vargs {-b 2 leftoverA leftoverB}
    } -body {
        array set opts [twapi::parseargs vargs {{b.arg}} -maxleftover 2]
        set vargs
    } -result "leftoverA leftoverB"

    test parseargs-7.3 {
        Verify parsing of leftover arguments (more than what's allowed)
    } -setup {
        unset -nocomplain opts
        set vargs {-b 2 leftoverA leftoverB}
    } -body {
        array set opts [twapi::parseargs vargs {{b.arg}} -maxleftover 3]
        set vargs
    } -result "leftoverA leftoverB"

    test parseargs-8.0 {
        Verify -hyphenated
    } -body {
        set vargs {-a arg -i 5 -b}
        twapi::parseargs vargs {
            a.arg
            i.int
            b.switch
            c.int
        } -nulldefault -hyphenated
    } -result {-a arg -i 5 -b 1 -c 0}

    test parseargs-9.0 {
        Verify large number of options
    } -setup {
        set largs {}
        set options {}
        set result {}
        for {set i 0} {$i < 100} {incr i} {
            lappend options a$i.arg i$i.int b$i.bool s$i.switch o$i
            lappend largs -a$i arg$i -i$i $i -b$i [expr {$i & 1}] -s$i -o$i
            lappend result a$i arg$i i$i $i b$i [expr {$i & 1}] s$i 1 o$i 1
        }
    } -body {
        equal_lists [twapi::parseargs largs $options] $result
    } -result 1

    test parseargs-10.0 {
        Verify -setvars large number of options
    } -body {
        # Do not want to pollute variable names so do in a lambda
        {*}[twapi::lambda {} {
            set largs {}
            set options {}
            set result {}
            for {set i 0} {$i < 100} {incr i} {
                lappend options a$i.arg i$i.int b$i.bool s$i.switch o$i
                lappend largs -a$i arg$i -i$i $i -b$i [expr {$i & 1}] -s$i -o$i
                lappend result a$i arg$i i$i $i b$i [expr {$i & 1}] s$i 1 o$i 1
            }
            twapi::parseargs largs $options -setvars
            foreach {var val} $result {
                if {[set $var] != $val} {
                    return 0
                }
            }
            return 1
        }]
    } -result 1

    test parseargs-10.1 {
        Verify -setvars vars not set
    } -body {
        # Do not want to pollute variable names so do in a lambda
        {*}[twapi::lambda {} {
            set largs {}
            twapi::parseargs largs {a.arg i.int b.bool s.switch o} -setvars
            foreach var {a i b} {
                if {[info exists $var]} {
                    return 0
                }
            }
            return 1
        }]
    } -result 1

    test parseargs-10.2 {
        Verify -setvars -nulldefault
    } -body {
        # Do not want to pollute variable names so do in a lambda
        {*}[twapi::lambda {} {
            set largs {}
            twapi::parseargs largs {a.arg i.int b.bool s.switch o} -setvars -nulldefault
            foreach {var val} {a "" i 0 b 0 s 0 o 0} {
                if {[set $var] != $val} {
                    return 0
                }
            }
            return 1
        }]
    } -result 1

    test parseargs-11.0 {
        Verify parsing termination with --
    } -setup {
        unset -nocomplain opts
        set vargs {-- -b leftoverA leftoverB}
    } -body {
        array set opts [twapi::parseargs vargs {b}]
        list $opts(b) $vargs
    } -result [list 0 "-b leftoverA leftoverB"]

    test parseargs-11.1 {
        Verify parsing termination with -
    } -setup {
        unset -nocomplain opts
        set vargs {- -b leftoverA leftoverB}
    } -body {
        array set opts [twapi::parseargs vargs {b}]
        list $opts(b) $vargs
    } -result [list 0 "-b leftoverA leftoverB"]

    test parseargs-11.2 {
        Verify parsing termination with -- and -maxleftover
    } -setup {
        unset -nocomplain opts
        set vargs {-a 1 -- -b 2 leftoverA leftoverB}
    } -body {
        array set opts [twapi::parseargs vargs {a.bool b.int} -maxleftover 4]
        list $opts(a) [info exists opts(b)] $vargs
    } -result [list 1 0 "-b 2 leftoverA leftoverB"]

    test parseargs-11.3 {
        Verify parsing termination with -- and -maxleftover
    } -setup {
        unset -nocomplain opts
        set vargs {-- -b 2 leftoverA leftoverB}
    } -body {
        array set opts [twapi::parseargs vargs {b.int} -maxleftover 3]
    } -result "Command has extra arguments specified: -b 2 leftoverA leftoverB" -returnCodes error

    test parseargs-11.4 {
        Verify parsing of invalid options with --
    } -body {
        unset -nocomplain opts
        set vargs {-b abc -- -a 2 extraarg}
        array set opts [twapi::parseargs vargs {b.arg c.int}]
        list $opts(b) [info exists opts(c)] $vargs
    } -result [list abc 0 "-a 2 extraarg"]

    test parseargs-11.5 {
        Verify parsing of invalid options with --
    } -body {
        unset -nocomplain opts
        set vargs {-a 1 -- -b 2 extraarg}
        array set opts [twapi::parseargs vargs {b.arg c.int}]
    } -returnCodes {error} -result "Invalid option '-a'. Must be one of -b, -c"

    test parseargs-11.6 {
        Verify parsing of invalid options with -- -nulldefault
    } -body {
        unset -nocomplain opts
        set vargs {-b abc -- -a 2 extraarg}
        array set opts [twapi::parseargs vargs {b.arg c.int} -nulldefault]
        list $opts(b) $opts(c) $vargs
    } -result [list abc 0 "-a 2 extraarg"]


    test parseargs-12.0 {
        Verify parsing of arguments of type sym
    } -body {
        unset -nocomplain opts
        set vargs {-a x -b n extraarg}
        array set opts [twapi::parseargs vargs {{a.sym x {x 0 y 1}} {b.sym n {m 2 n 3}} {c.sym i {i 4 j 5}}}]
        expr {
              [llength $vargs] == 1 &&
              [string equal [lindex $vargs 0] "extraarg"] &&
              $opts(a) == 0 && $opts(b) == 3 && $opts(c) == 4}
    } -result 1

    test parseargs-12.1 {
        Verify parsing of arguments of type sym not matching enumerated values
    } -body {
        unset -nocomplain opts
        set vargs {-a x -b o}
        array set opts [twapi::parseargs vargs {{a.sym x {x 0 y 1}} {b.sym n {m 2 n 3}} {c.sym i {i 4 j 5}}}]
    } -returnCodes {error} -result "Invalid value 'o' specified for option '-b'."

    ################################################################

    ::tcltest::cleanupTests
}

namespace delete ::twapi::parseargs::test
